buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.github.johnrengelman.shadow'

version = "0.1"
group = "io.github.tivj.teemutweaks"
archivesBaseName = "TeemuTweaksForge"

sourceCompatibility = targetCompatibility = 1.8

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    makeObfSourceJar = false

    replaceIn "TTMain.java"
    replace "@MOD_VERSION@", project.version
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    //maven { url = 'https://repo.sk1er.club/repository/maven-public' }
}

dependencies {
    implementation 'com.github.HypixelDev:PublicAPI:master-SNAPSHOT'

    implementation('com.github.KevinPriv:MojangAPI:1.0') {
        exclude module: "gson"
    }

    //implementation "club.sk1er:Vigilance:1.1.3-10809"
}

shadowJar {
    baseName = 'TeemuTweaks '+project.version+" for 1 8 9 at "+new Date().format('dd-MM-yyyy')
    classifier = ''
}

reobf {
    shadowJar {} // Reobfuscate the shadowed JAR
}

task moveResources {
    doLast {
        ant.move file: "${buildDir}/resources/main",
                todir: "${buildDir}/classes/java"
    }
}

moveResources.dependsOn processResources
classes.dependsOn moveResources

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
    rename '(.+_at.cfg)', 'META-INF/$1'
}

jar {
    baseName = project.archivesBaseName + " unshadowed"
    manifest {
        attributes(
                "FMLCorePlugin": "io.github.tivj.teemutweaks.asm.tweaker.Tweaker",
                "ForceLoadAsMod": true,
                "FMLCorePluginContainsFMLMod": true,
                "ModSide": "CLIENT",
                'FMLAT': 'tttest_at.cfg'
        )
    }
}